/****************************************************************************
MIT License

Copyright (c) 2024 Guillaume Boiss√©

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
****************************************************************************/
#define PI 3.141592653589793238463f

uint     g_MipLevel;
uint     g_FaceIndex;
float4x4 g_ViewProjectionInverse;

TextureCube g_Cubemap;
Texture2D   g_EnvironmentMap;

RWTexture2DArray<float4> g_InCubemap;
RWTexture2DArray<float4> g_OutCubemap;
RWTexture2D<float2>      g_BrdfBuffer;
RWTexture2DArray<float4> g_IrradianceBuffer;
RWTexture2DArray<float4> g_EnvironmentBuffer;

SamplerState g_LinearSampler;

// https://en.wikipedia.org/wiki/Equirectangular_projection
float2 SampleSphericalMap(in float3 rd)
{
    return float2(atan2(rd.z, rd.x) / (2.0f * PI) + 0.5f, 1.0f - acos(rd.y) / PI);
}

[numthreads(8, 8, 1)]
void DrawCubemap(in uint2 did : SV_DispatchThreadID)
{
    uint3 dimensions;
    g_OutCubemap.GetDimensions(dimensions.x, dimensions.y, dimensions.z);

    float2 uv  = (did + 0.5f) / dimensions.xy;
    float2 ndc = 2.0f * uv - 1.0f;

    float4 world = mul(g_ViewProjectionInverse, float4(ndc, 1.0f, 1.0f));
    world /= world.w;   // perspective divide

    uv = SampleSphericalMap(normalize(world.xyz));

    float3 color = g_EnvironmentMap.SampleLevel(g_LinearSampler, uv, 0.0f).xyz;

    g_OutCubemap[int3(did, g_FaceIndex)] = float4(color, 1.0f);
}

[numthreads(8, 8, 1)]
void BlurCubemap(in uint3 did : SV_DispatchThreadID)
{
    uint3 dimensions;
    g_InCubemap.GetDimensions(dimensions.x, dimensions.y, dimensions.z);

    if(any(did.xy >= max(dimensions.xy >> 1, 1)))
    {
        return; // out of bounds
    }

    float4 result       = float4(0.0f, 0.0f, 0.0f, 0.0f);
    float  sample_count = 0.0f;

    for(uint y = 0; y < 2; ++y)
    {
        for(uint x = 0; x < 2; ++x)
        {
            uint2 pix = (did.xy << 1) + uint2(x, y);

            if(any(pix >= dimensions.xy))
            {
                break;  // out of bounds
            }

            result += g_InCubemap[int3(pix, did.z)];

            sample_count += 1.0f;
        }
    }

    g_OutCubemap[did] = result / sample_count;
}

//!
//! For more info, see: https://cdn2.unrealengine.com/Resources/files/2013SiggraphPresentationsNotes-26915738.pdf
//!

[numthreads(8, 8, 1)]
void DrawIrradiance(in uint2 did : SV_DispatchThreadID)
{
    uint3 dimensions;
    g_IrradianceBuffer.GetDimensions(dimensions.x, dimensions.y, dimensions.z);

    float2 uv  = (did + 0.5f) / dimensions.xy;
    float2 ndc = 2.0f * uv - 1.0f;

    float4 world = mul(g_ViewProjectionInverse, float4(ndc, 1.0f, 1.0f));
    world /= world.w;   // perspective divide

    float3 forward = normalize(world.xyz);
    float3 up = float3(0.0f, 1.0f, 0.0f);
    float3 right = cross(up, forward);
    up = cross(forward, right);

    float sample_count = 0.0f;
    float sample_delta = 0.025f;

    float3 irradiance = float3(0.0f, 0.0f, 0.0f);

    for(float phi = 0.0f; phi < 2.0f * PI; phi += sample_delta)
    {
        for(float theta = 0.0f; theta < 0.5f * PI; theta += sample_delta)
        {
            // Spherical to cartesian (in tangent space)
            float3 tangent_sample = float3(sin(theta) * cos(phi),  sin(theta) * sin(phi), cos(theta));
            // Tangent space to world
            float3 sample_vector = tangent_sample.x * right + tangent_sample.y * up + tangent_sample.z * forward;
            // Accumulate irradiance contribution
            irradiance += g_Cubemap.SampleLevel(g_LinearSampler, sample_vector, 0.0f).xyz * cos(theta) * sin(theta);
            // Increment sample count
            sample_count += 1.0f;
        }
    }

    g_IrradianceBuffer[int3(did, g_FaceIndex)] = float4(PI * irradiance / sample_count, 1.0f);
}

float VanDerCorput(in uint bits)
{
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(double(bits) * 2.3283064365386963e-10);    // / 0x100000000
}

float2 Hammersley(in uint i, in uint N)
{
    return float2(i / float(N), VanDerCorput(i));
}

float3 ImportanceSampleGGX(in float2 Xi, in float3 N, in float roughness)
{
    float a = roughness * roughness;

    float phi       = 2.0f * PI * Xi.x;
    float cos_theta = sqrt((1.0f - Xi.y) / (1.0f + (a * a - 1.0f) * Xi.y));
    float sin_theta = sqrt(1.0f - cos_theta * cos_theta);

    // From spherical coordinates to cartesian coordinates
    float3 H;
    H.x = cos(phi) * sin_theta;
    H.y = sin(phi) * sin_theta;
    H.z = cos_theta;

    // From tangent-space vector to world-space sample vector
    float3 up        = (abs(N.z) < 0.999f ? float3(0.0f, 0.0f, 1.0f) : float3(1.0f, 0.0f, 0.0f));
    float3 tangent   = normalize(cross(up, N));
    float3 bitangent = cross(N, tangent);

    return normalize(tangent * H.x + bitangent * H.y + N * H.z);
}

float DistributionGGX(in float n_dot_h, in float roughness)
{
    float a               = roughness * roughness;
    float a_squared       = a * a;
    float n_dot_h_squared = n_dot_h * n_dot_h;

    float denom = (n_dot_h_squared * (a_squared - 1.0f) + 1.0f);

    denom = PI * denom * denom;

    return a_squared / denom;
}

[numthreads(8, 8, 1)]
void DrawEnvironment(in uint2 did : SV_DispatchThreadID)
{
    uint3 dimensions;
    g_EnvironmentBuffer.GetDimensions(dimensions.x, dimensions.y, dimensions.z);

    float2 uv  = (did + 0.5f) / dimensions.xy;
    float2 ndc = 2.0f * uv - 1.0f;

    float4 world = mul(g_ViewProjectionInverse, float4(ndc, 1.0f, 1.0f));
    world /= world.w;   // perspective divide

    // In split-sum approximation, reflection and view vector are the same
    float3 N = normalize(world.xyz);
    float3 R = N;
    float3 V = R;

    float  total_weight = 0.0f;
    float3 environment  = float3(0.0f, 0.0f, 0.0f);

    const uint  sample_count = 4096;
    const float roughness    = g_MipLevel / 4.0f;

    for(uint i = 0; i < sample_count; ++i)
    {
        float2 Xi = Hammersley(i, sample_count);
        float3 H  = ImportanceSampleGGX(Xi, N, roughness);
        float3 L  = normalize(2.0f * dot(V, H) * H - V);

        float n_dot_l = max(dot(N, L), 0.0f);

        if(n_dot_l > 0.0f)
        {
            // Sample from the environment's mip level based on roughness/pdf
            float nDotH = max(dot(N, H), 0.0f);
            float hDotV = max(dot(H, V), 0.0f);
            float D     = DistributionGGX(nDotH, roughness);
            float pdf   = D * nDotH / (4.0f * hDotV) + 0.0001f;

            // Compute mip level to be sampled
            float sa_texel  = 4.0f * PI / (6.0f * dimensions.x * dimensions.y);
            float sa_sample = 1.0f / (sample_count * pdf + 0.0001f);
            float mip_level = (roughness == 0.0f ? 0.0f : 0.5f * log2(sa_sample / sa_texel));

            // Add contribution to convolution
            environment  += g_Cubemap.SampleLevel(g_LinearSampler, L, mip_level).xyz * n_dot_l;
            total_weight += n_dot_l;
        }
    }

    g_EnvironmentBuffer[int3(did, g_FaceIndex)] = float4(environment / total_weight, 1.0f);
}

float GeometrySchlickGGX(in float n_dot_v, in float roughness)
{
    float a = roughness * roughness;
    float k = a / 2.0f;

    float denom = n_dot_v * (1.0f - k) + k;

    return n_dot_v / denom;
}

float GeometrySmith(in float n_dot_v, in float n_dot_l, in float roughness)
{
    float ggx2 = GeometrySchlickGGX(n_dot_v, roughness);
    float ggx1 = GeometrySchlickGGX(n_dot_l, roughness);

    return ggx1 * ggx2;
}

[numthreads(8, 8, 1)]
void DrawBRDF(in uint2 did : SV_DispatchThreadID)
{
    uint2 dimensions;
    g_BrdfBuffer.GetDimensions(dimensions.x, dimensions.y);

    float2 uv = (did + 0.5f) / dimensions;

    float n_dot_v   = uv.x;
    float roughness = uv.y;

    float3 N = float3(0.0f, 0.0f, 1.0f);
    float3 V = float3(sqrt(1.0f - n_dot_v * n_dot_v), 0.0f, n_dot_v);

    float A = 0.0f;
    float B = 0.0f;

    const uint sample_count = 1024;

    for(uint i = 0; i < sample_count; ++i)
    {
        float2 Xi = Hammersley(i, sample_count);
        float3 H  = ImportanceSampleGGX(Xi, N, roughness);
        float3 L  = normalize(2.0f * dot(V, H) * H - V);

        float n_dot_l = max(L.z, 0.0f);
        float n_dot_h = max(H.z, 0.0f);
        float v_dot_h = max(dot(V, H), 0.0f);

        if(n_dot_l > 0.0f)
        {
            float G     = GeometrySmith(n_dot_v, n_dot_l, roughness);
            float G_Vis = (G * v_dot_h) / (n_dot_h * n_dot_v);
            float Fc    = pow(1.0f - v_dot_h, 5.0f);

            A += (1.0f - Fc) * G_Vis;
            B += Fc * G_Vis;
        }
    }

    g_BrdfBuffer[did] = float2(A, B) / sample_count;
}
